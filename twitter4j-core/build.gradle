plugins {
    id 'java'
    id 'signing'
    id 'maven-publish'
}

group 'org.twitter4j'
version '4.0.8-SNAPSHOT'

repositories {
    mavenCentral()
}
sourceSets {
    main {
        java {
            srcDirs(['src/http/java',
                     'src/jmx/java',
                     'src/json/java',
                     'src/logging/java',
                     'src/stream/java',
                     'src/v1/java',
                     'src/main/java'])
        }
    }
    test {
        java {
            srcDirs(['src/test/java',
                     'src/testv1/java'])
        }
    }

}

task sourcesJar(type: Jar) {
    duplicatesStrategy = 'include'
    classifier = 'sources'
    from sourceSets.main.allSource
}

java {
    withJavadocJar()
    withSourcesJar()
}
compileJava   {
    sourceCompatibility = '8'
    targetCompatibility = '8'
}

compileTestJava   {
    sourceCompatibility = '17'
    targetCompatibility = '17'
}

dependencies {
    compileOnly 'org.slf4j:slf4j-api:2.0.2'
    compileOnly 'org.apache.logging.log4j:log4j-core:2.19.0'
    implementation 'org.jetbrains:annotations:23.0.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.1'
    testImplementation 'org.seleniumhq.selenium:htmlunit-driver:3.64.0'
    testImplementation 'com.googlecode:kryo:1.04'
    testRuntimeOnly 'ch.qos.logback:logback-core:1.4.1'
    testRuntimeOnly 'ch.qos.logback:logback-classic:1.4.1'

}

test {
    useJUnitPlatform()
}

jar {
    from('module-info/build/classes/java/main') {
        include "module-info.class"
        into "META-INF/versions/9"
    }
    manifest {
        attributes 'Multi-Release': 'true'
    }
}
tasks.jar.dependsOn([':twitter4j-core:module-info:compileJava'])

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'twitter4j-core'
            from components.java
            pom {
                name = 'twitter4j-core'
                description = 'twitter4j'
                url = 'https://github.com/Twitter4J/Twitter4J'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'yusuke'
                        name = 'Yusuke Yamamoto'
                        email = 'yusuke@mac.com'
                    }
                }
                scm {
                    url = 'https://github.com/Twitter4J/Twitter4J/'
                    connection = 'scm:git:git://github.com/Twitter4J/Twitter4J.git'
                    developerConnection = 'scm:git:git@github.com:Twitter4J/Twitter4J.git'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = project.properties['SONATYPE_USERNAME'] ?: "none"
                password = project.properties['SONATYPE_PASSWORD'] ?: "none"
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
