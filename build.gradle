plugins {
    id 'java'
    id 'signing'
    id 'maven-publish'
}

group 'org.twitter4j'
version '4.1.2'

repositories {
    mavenCentral()
}

java {
    withJavadocJar()
    withSourcesJar()
}

compileJava   {
    sourceCompatibility = '17'
    targetCompatibility = '17'
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'twitter4j'
            from components.java
            pom {
                name = 'twitter4j'
                description = 'twitter4j'
                url = 'https://github.com/Twitter4J/Twitter4J'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'yusuke'
                        name = 'Yusuke Yamamoto'
                        email = 'yusuke@mac.com'
                    }
                }
                scm {
                    url = 'https://github.com/Twitter4J/Twitter4J/'
                    connection = 'scm:git:git://github.com/Twitter4J/Twitter4J.git'
                    developerConnection = 'scm:git:git@github.com:Twitter4J/Twitter4J.git'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = project.properties['SONATYPE_USERNAME'] ?: "none"
                password = project.properties['SONATYPE_PASSWORD'] ?: "none"
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
